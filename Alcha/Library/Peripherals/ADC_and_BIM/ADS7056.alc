class ADS7056(net Clk, net Reset){
  // Maximum SPI clock is 60 MHz, and gives a 2.5 MSps sampling rate
  assert(Clk'frequency <= 120e6);
  //----------------------------------------------------------------------------

  private{
    net nCS  = 1;
    net SClk = 0;
    net Data;

    net(14) Shift;
    net( 6) Count = 0;
  }
  //----------------------------------------------------------------------------

  output group Output{
    net(14) Data;
    net     Valid = 0;
  }
  //----------------------------------------------------------------------------

  void ConnectPins(pin nCS, pin SClk, pin SDO){
    nCS  = this.nCS;
    SClk = this.SClk;
    Data = SDO;

    SClk'create_generated_clock = <divide_by = 2, source = Clk>;
    
    // The state machine takes care of the actual sample and hold timing, so
    // this just needs to make sure it's not ridiculously out of place.  The
    // constraints below ensures that the signals change within a 5 Î¼s window,
    // 3 ns after the clock edge.
    nCS'set_output_delay'max +=  0e-9; // Setup
    nCS'set_output_delay'min += -3e-9; // Hold

    // The actual IC delay is 2.5 to 10 ns, but the state machine ensures that
    // the data is sampled just before making SClk high.
    SDO'set_input_delay'min +=  2.5e-9;
    SDO'set_input_delay'max += 10.0e-9;

    SDO'set_multicycle_path'to    = Clk;
    SDO'set_multicycle_path'setup = 2;
    SDO'set_multicycle_path'hold  = 1;
  }
  //----------------------------------------------------------------------------

  rtl(Clk, Reset){
    if(Count == 47) Count = 0;
    else            Count++;

    switch(Count){
      case(0    ) nCS  = 0;
      case(1..36) SClk = ~SClk;
      case(37   ) nCS  = 1;
      default     SClk = 0; // Reset in case something went wrong
    }

    if(SClk == 0) Shift = :(Shift[12..0], Data);

    if(Count == 32){
      Output.Data  = Shift;
      Output.Valid = 1;
    }else{
      Output.Valid = 0;
    }
  }
}
//------------------------------------------------------------------------------

